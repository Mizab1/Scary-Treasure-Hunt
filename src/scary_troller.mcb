import ./templates/raycast.mcbt

dir private {
    
    #! Main functions
    function on_load minecraft:load{
        scoreboard objectives add mcb.internal dummy
        scoreboard objectives add rc_detect minecraft.used:minecraft.carrot_on_a_stick
        scoreboard objectives add item_select dummy

        scoreboard objectives add pos_x1 dummy
        scoreboard objectives add pos_y1 dummy
        scoreboard objectives add pos_z1 dummy

        scoreboard objectives add pos_x2 dummy
        scoreboard objectives add pos_y2 dummy
        scoreboard objectives add pos_z2 dummy

    }
    function on_tick minecraft:tick {
        execute as @a[scores={rc_detect=1..}] at @s run{

            # Trap diffuser setup
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"trap_diffuser"}}}}] run{
                <%%
                    global["diffuser_radius"] = 2
                %%>
                kill @e[tag=scary_generated, distance=..<%global["diffuser_radius"]%>]
                fill ~-<%global["diffuser_radius"]/2%> ~-<%global["diffuser_radius"]/2%> ~-<%global["diffuser_radius"]/2%> ~<%global["diffuser_radius"]/2%> ~<%global["diffuser_radius"]/2%> ~<%global["diffuser_radius"]/2%> air replace #scary_troller:scary_blocks
            }

            # Random teleport logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"random_teleport_trap"}}}}, tag=!used_teleport_trap] run{
                spreadplayers ~ ~ 10 25 false @a[tag=!troller]
                tag @s add used_teleport_trap
            } else execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"random_teleport_trap"}}}}, tag=used_teleport_trap] run {
                tellraw @s {"text":"Slow down! you are using this trap too frequently...", "color":"red"}
            }

            # Scary Sound 1 logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"lidcreak_sound_trap"}}}}] run{
                playsound minecraft:scary.ambient.lidcreak master @a ~ ~ ~ 
            }

            # Scary Sound 2 logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"dark_slam_sound_trap"}}}}] run{
                playsound minecraft:scary.ambient.dark_slam master @a ~ ~ ~ 
            }

            # Scary Sound 3 logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"spooky_ambience_sound_trap"}}}}] run{
                playsound minecraft:scary.ambient.spooky_ambience master @a ~ ~ ~ 
            }

            # Scary Sound 4 logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"supernatural_sound_trap"}}}}] run{
                playsound minecraft:scary.ambient.supernatural master @a ~ ~ ~ 
            }

            # Freeze players trap logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"freeze_trap"}}}}, tag=!used_freeze_trap] run{
                <%%
                    global["freeze_player_trap_reach"] = 15
                    global["freeze_player_trap_duration"] = 4
                %%>
                execute as @a[tag=!troller, distance=..<%global["freeze_player_trap_reach"]%>] at @s run{
                    summon armor_stand ~ ~ ~ {Invisible:1b,Tags:["cage","scary_generated"],ArmorItems:[{},{},{},{id:"minecraft:wooden_hoe",count:1,components:{"minecraft:item_model":"cage"}}]}
                    effect give @s minecraft:slowness <%global["freeze_player_trap_duration"]%> 100 true
                    schedule <%global["freeze_player_trap_duration"]%>s replace {
                        kill @e[type=armor_stand,tag=cage]
                    }

                }
                tag @s add used_freeze_trap
            } else execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"freeze_trap"}}}}, tag=used_freeze_trap] run {
                tellraw @s {"text":"Slow down! You have just used this trap.", "color":"red"}
            }

            # Levitation wand logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"levitation_wand"}}}}] run{
                raycast levitation @s #minecraft:air @a[distance=..2,tag=!troller] {
                    effect give @a[distance=..2,tag=!troller] levitation 1 20 true
                }
            }

            # Blindness wand logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"blindness_wand"}}}}] run{
                raycast blindness @s #minecraft:air @a[distance=..2,tag=!troller] {
                    effect give @a[distance=..2,tag=!troller] darkness 4 1 true
                }
            }

            # Snowball gun logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"snowball_gun"}}}}] run{
                summon snowball ^ ^1.5 ^ {Tags:["custom_snowball"]}
            }
            execute as @e[type=snowball,tag=custom_snowball,tag=!processed] at @s rotated as @p run{
                execute store result score @s pos_x1 run data get entity @s Pos[0] 1000
                execute store result score @s pos_y1 run data get entity @s Pos[1] 1000
                execute store result score @s pos_z1 run data get entity @s Pos[2] 1000

                tp @s ^ ^ ^0.1

                execute store result score @s pos_x2 run data get entity @s Pos[0] 1000
                execute store result score @s pos_y2 run data get entity @s Pos[1] 1000
                execute store result score @s pos_z2 run data get entity @s Pos[2] 1000

                scoreboard players operation @s pos_x2 -= @s pos_x1
                scoreboard players operation @s pos_y2 -= @s pos_y1
                scoreboard players operation @s pos_z2 -= @s pos_z1 

                execute store result entity @s Motion[0] double 0.03 run scoreboard players get @s pos_x2
                execute store result entity @s Motion[1] double 0.03 run scoreboard players get @s pos_y2
                execute store result entity @s Motion[2] double 0.03 run scoreboard players get @s pos_z2 

                tag @s add processed
            }

            # Backward and forward button
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"previous_arrow"}}}}] run{
                execute if score @s item_select matches 1.. run{
                    scoreboard players remove @s item_select 1
                    playsound minecraft:block.stone_button.click_off master @s ~ ~ ~ 1 1
                    function *private/cycle_items/cycle_item_check
                }
                execute if score @s item_select matches 0 run{
                    playsound minecraft:block.note_block.didgeridoo master @s ~ ~ ~ 0.5 1.2
                }
            }
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"next_arrow"}}}}] run{
                execute if score @s item_select matches ..<%global["ITEMS_LENGTH"] - 8%> run{
                    scoreboard players add @s item_select 1
                    playsound minecraft:block.stone_button.click_on master @s ~ ~ ~ 1 1
                    function *private/cycle_items/cycle_item_check
                }
                execute if score @s item_select matches <%global["ITEMS_LENGTH"] - 8 + 1%> run{
                    playsound minecraft:block.note_block.didgeridoo master @s ~ ~ ~ 0.5 1.2
                }
            }

            # Reset click
            scoreboard players reset @s rc_detect
        }
    } 

    #! Clocks
    dir clocks {

        clock clock_160t 160t{
            tag @a[tag=used_freeze_trap] remove used_freeze_trap
        }
        clock clock_80t 80t{
            tag @a[tag=used_teleport_trap] remove used_teleport_trap
        }
        clock clock_20t 20t {
            # Hole block logic
            execute as @e[type=armor_stand, tag=hole_block_marker] at @s run{
                execute if entity @a[tag=!troller, distance=..3] unless entity @e[type=silverfish, tag=hole_block_worm, distance=..3] run{
                    summon silverfish ~ ~1 ~ {Tags:["hole_block_worm", "scary_generated"],CustomName:'"Blood Worm"'}
                }
                execute unless block ~ ~ ~ orange_glazed_terracotta run{
                    kill @s
                }
            }

            # Water trap logic
            execute as @e[type=marker, tag=water_trap] at @s run{
                execute if entity @a[tag=!troller, distance=..1] run{
                    REPEAT (-1, 1) as i {
                        REPEAT (-1, 1) as j {
                            summon evoker_fangs ~<%i%> ~ ~<%j%>
                        }
                    }
                }
            }
        }

        clock clock_10t 10t {
            # Creeper trap logic
            execute as @e[type=marker, tag=creeper_trap_marker] at @s run{
                particle dust{color:[1.000,0.000,0.000],scale:1} ~ ~ ~ .1 .1 .1 1 2 normal
                execute if entity @a[tag=!troller, distance=..3] run{
                    summon creeper ~ ~ ~ {DeathLootTable:"empty", Tags:["scary_generated"]}
                    REPEAT (1, 4) {
                        summon vex ~ ~ ~ {DeathLootTable:"empty",Tags:["scary_generated"],HandItems:[{id:"minecraft:air",count:1},{}],HandDropChances:[0.000F,0.000F]}
                    }

                    # Kill the marker
                    kill @s
                }
            }

            # Guardian trap logic
            execute as @e[type=marker, tag=guardian_trap_marker] at @s run{
                particle dust{color:[0.000,1.000,0.000],scale:1} ~ ~ ~ .1 .1 .1 1 2 normal
                execute if entity @a[tag=!troller, distance=..3] run{
                    particle minecraft:elder_guardian ~ ~ ~ 0 0 0 1 1 normal @a[distance=..3]
                    playsound minecraft:entity.elder_guardian.curse master @a ~ ~ ~ 2 0.3
                    kill @s
                }
            }

            # Landmine trap logic
            execute as @e[type=armor_stand, tag=landmine] at @s run{
                execute if entity @a[tag=!troller, distance=..2] run{
                    summon creeper ~ ~ ~ {Fuse:0, Tags:["scary_generated"]}
                    kill @s
                }
            }

            # Fire stone logic 
            execute as @e[type=armor_stand, tag=fire_stone] at @s run{
                execute unless block ~ ~ ~ light_blue_terracotta run{
                    fill ~3 ~3 ~3 ~-3 ~-3 ~-3 fire replace air
                    kill @s
                }
            }
        }

        clock clock_5t 5t {
            # Setup the creeper trap
            execute as @e[type=endermite, tag=creeper_trap_spawner_marker] at @s run{
                summon marker ~ ~ ~ {Tags:["creeper_trap_marker", "scary_generated"]}
                kill @s
            }

            # Setup the guardian trap
            execute as @e[type=endermite, tag=guardian_trap_spawner_marker] at @s run{
                summon marker ~ ~ ~ {Tags:["guardian_trap_marker", "scary_generated"]}
                kill @s
            }

            # Setup the hole block
            execute as @e[type=endermite, tag=hole_block_spawner_marker] at @s run{
                setblock ~ ~ ~ orange_glazed_terracotta
                summon armor_stand ~ ~ ~ {Invisible:1b,Marker:1b,Tags:["hole_block_marker", "scary_generated"]}
                kill @s
            }

            # Setup the landmine
            execute as @e[type=endermite, tag=landmine_spawner_marker] at @s run{
                summon armor_stand ~ ~ ~ {Invisible:1b,Tags:["landmine", "scary_generated"],ArmorItems:[{},{},{},{id:"minecraft:endermite_spawn_egg",count:1,components:{"minecraft:item_model":"landmine_trap"}}]}
                kill @s
            }

            # Setup the fire stone
            execute as @e[type=endermite, tag=fire_stone_spawner_marker] at @s run{
                setblock ~ ~ ~ light_blue_terracotta
                summon armor_stand ~ ~ ~ {Invisible:1b,Tags:["fire_stone", "scary_generated"], Marker:1b}
                kill @s
            }

            # Setup the water trap
            execute as @e[type=endermite, tag=water_spawner_marker] at @s run{
                execute if block ~ ~ ~ water run{
                    summon marker ~ ~ ~ {Tags:["water_trap", "scary_generated"]}
                    kill @s
                } else run {
                    tellraw @a[tag=troller] {"text":"[Water Trap] Trap can't be set up because it's not in a water body", "color":"red"}
                    kill @s
                }
            }

            # Setup the herobrine trap
            execute as @e[type=endermite, tag=herobrine_spawner_marker] at @s run{
                summon armor_stand ~ ~ ~ {Invisible:1b,Tags:["new","herobrine_trap", "scary_generated"], ArmorItems:[{},{},{},{id:"minecraft:wooden_hoe",count:1,components:{"minecraft:item_model":"herobrine_trap"}}]}
                execute as @e[type=armor_stand, tag=herobrine_trap, tag=new, distance=..3] at @s run{
                    rotate @s facing entity @p[tag=troller]
                    tag @s remove new
                }
                kill @s
            }
        }
    }

    #! Forward and Backward stick handler
    <%%
        let items = [
            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["creeper_trap_spawner_marker"]},custom_name='{"italic":false,"text":"Creeper Trap"}',lore=['"Summons a creeper when the player is nearby"'],custom_data={tool:"creeper_trap"},minecraft:item_model=creeper_trap] `,

            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["guardian_trap_spawner_marker"]},custom_name='{"italic":false,"text":"Guardian Trap"}',lore=['"Displays the guardian particle to the nearby player"'],custom_data={tool:"guardian_trap"},minecraft:item_model=guardian_trap] `,
            
            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["hole_block_spawner_marker"]},custom_name='{"italic":false,"text":"Hole Block"}',lore=['"Spawns a worm when a"','"player passes by"'],custom_data={tool:"hole_block_trap"},minecraft:item_model=hole_block_trap] `,

            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["landmine_spawner_marker"]},custom_name='{"italic":false,"text":"Landmine"}',lore=['"Places a landmine which explodes"','"when a player steps on it"'],custom_data={tool:"landmine_trap"},minecraft:item_model=landmine_trap] `,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Trap Diffuser"}',lore=['"Disable all the traps within ${global["diffuser_radius"]} blocks radius."'],custom_data={tool:"trap_diffuser"},minecraft:item_model=trap_diffuser]`,

            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["fire_stone_spawner_marker"]},custom_name='{"italic":false,"text":"Fire Stone"}',lore=['"Places a stone which when mined sets"','"the nearby area ablaze"'],custom_data={tool:"fire_stone_trap"},minecraft:item_model=fire_stone_trap] `,

            `stick[enchantment_glint_override=true,custom_name='{"italic":false,"text":"Knockback Stick"}',lore=['"Deals humongous amount of knockback"','"to enemies"'],enchantments={levels:{"minecraft:knockback":25},show_in_tooltip:false}]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Random Teleport"}',lore=['"Randomly teleport all the enemies."'],custom_data={tool:"random_teleport_trap"},minecraft:item_model=random_teleport_trap]`,

            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["water_spawner_marker"]},custom_name='{"italic":false,"text":"Water Trap"}',lore=['"Summons fangs in water when a player is nearby"','"Can only be placed in water"'],custom_data={tool:"water_trap"},minecraft:item_model=water_trap] `,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Play Scary Sound 1"}',lore=['"Right-click to play a scary sound at your position."'],custom_data={tool:"lidcreak_sound_trap"},minecraft:item_model=lidcreak_sound_trap]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Play Scary Sound 2"}',lore=['"Right-click to play a scary sound at your position."'],custom_data={tool:"dark_slam_sound_trap"},minecraft:item_model=dark_slam_sound_trap]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Play Scary Sound 3"}',lore=['"Right-click to play a scary sound at your position."'],custom_data={tool:"spooky_ambience_sound_trap"},minecraft:item_model=spooky_ambience_sound_trap]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Play Scary Sound 4"}',lore=['"Right-click to play a scary sound at your position."'],custom_data={tool:"supernatural_sound_trap"},minecraft:item_model=supernatural_sound_trap]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Cage Trap"}',lore=['"Right-click to cage nearby players,"', '"up to ${global["freeze_player_trap_reach"]} blocks."'],custom_data={tool:"freeze_trap"},minecraft:item_model=freeze_trap]`,

            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["herobrine_spawner_marker"]},custom_name='{"italic":false,"text":"Herobrine Trap"}',lore=['"Summons herobrine statue when a player is nearby"','"Rotated as the troller"'],custom_data={tool:"herobrine_trap"},minecraft:item_model=herobrine_trap] `,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Levitation Wand"}',lore=['"Right-click to levitate the player you are aiming at"'],custom_data={tool:"levitation_wand"},minecraft:item_model=levitation_wand]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Blindness Wand"}',lore=['"Right-click to blind the player you are aiming at"'],custom_data={tool:"blindness_wand"},minecraft:item_model=blindness_wand]`,
            ]
        global["ITEMS_LENGTH"] = items.length
        global["ITEMS"] = items
        console.log(global["ITEMS_LENGTH"] - 8) + 1
    %%>
    dir cycle_items{
        function cycle_item_check{
            # There are 9 slots for every player, 2 slots are reserved for the left and right arrows, Hence, 9 - 2 = 7 slots are available
            REPEAT(0, global["ITEMS_LENGTH"] - 7) as i{
                execute as @a[tag=troller] at @s if score @s item_select matches <%i%> run{
                    <%%
                        for (let j = 1; j <= 7; j++) {
                            emit(`item replace entity @s hotbar.${j} with ${global["ITEMS"][(j+i)-1]}`)
                        }
                    %%>
                }
            }
        }
        function give_items{
            item replace entity @s hotbar.0 with carrot_on_a_stick[minecraft:item_model=previous_arrow, custom_data={tool:"previous_arrow"}, minecraft:item_name='{"text":"Previous"}'] 1
            scoreboard players set @s item_select 0
            function ./cycle_item_check
            item replace entity @s hotbar.8 with carrot_on_a_stick[minecraft:item_model=next_arrow, custom_data={tool:"next_arrow"}, minecraft:item_name='{"text":"Next"}'] 1
        }
    }
    # function test {
    #     <%%
    #         global["Item"] = "Hello";
    #         console.log(global)
    #     %%>
    #     <%%
    #         console.log(global)
    #     %%>
    # }
}

function make_me_troller{
    tag @s add troller
    function *private/cycle_items/give_items
}
function give_non_troll_weapons{
    give @s carrot_on_a_stick[custom_name='{"italic":false,"text":"Snowball Gun"}',lore=['"Right-click to shoot snowball"'],custom_data={tool:"snowball_gun"},item_model="snowball_gun"] 1
}

#! JSONs
tag blocks scary_blocks replace {
    orange_glazed_terracotta
    light_blue_terracotta
}