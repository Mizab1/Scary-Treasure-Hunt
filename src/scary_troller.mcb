#! Main functions
function on_load minecraft:load{
    scoreboard objectives add mcb.internal dummy
    scoreboard objectives add rc_detect minecraft.used:minecraft.carrot_on_a_stick
    scoreboard objectives add item_select dummy

}
function on_tick minecraft:tick {
    execute as @a[scores={rc_detect=1..}] at @s run{

        # Creeper trap setup
        execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_model_data":100001}}}] run{
            summon marker ~ ~ ~ {Tags:["creeper_trap_marker"]}
        }

        # Guardian trap setup
        execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_model_data":100002}}}] run{
            summon marker ~ ~ ~ {Tags:["guardian_trap_marker"]}
        }

        # Backward and forward button
        execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_model_data":111001}}}] run{
            execute if score @s item_select matches 1.. run{
                scoreboard players remove @s item_select 1
                playsound minecraft:block.stone_button.click_off master @s ~ ~ ~ 1 1
                function *cycle_items/cycle_item_check
            }
            execute if score @s item_select matches 0 run{
                playsound minecraft:block.note_block.didgeridoo master @s ~ ~ ~ 0.5 1.2
            }
        }
        execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_model_data":111002}}}] run{
            execute if score @s item_select matches ..<%global["ITEMS_LENGTH"] - 8%> run{
                scoreboard players add @s item_select 1
                playsound minecraft:block.stone_button.click_on master @s ~ ~ ~ 1 1
                function *cycle_items/cycle_item_check
            }
            execute if score @s item_select matches <%global["ITEMS_LENGTH"] - 8 + 1%> run{
                playsound minecraft:block.note_block.didgeridoo master @s ~ ~ ~ 0.5 1.2
            }
        }

        # Reset click
        scoreboard players reset @s rc_detect
    }
} 

#! Clocks
dir clocks {

    clock clock_20t 20t {
        # Hole block logic
        execute as @e[type=armor_stand, tag=hole_block_marker] at @s run{
            execute if entity @a[tag=!troller, distance=..3] unless entity @e[type=silverfish, tag=hole_block_worm, distance=..3] run{
                summon silverfish ~ ~1 ~ {Tags:["hole_block_worm"],CustomName:'"Blood Worm"'}
            }
        }
    }

    clock clock_10t 10t {
        # Creeper trap logic
        execute as @e[type=marker, tag=creeper_trap_marker] at @s run{
            particle dust{color:[1.000,0.000,0.000],scale:1} ~ ~ ~ .1 .1 .1 1 2 normal
            execute if entity @a[tag=!troller, distance=..3] run{
                summon creeper ~ ~ ~ {DeathLootTable:"empty"}
                REPEAT (1, 4) {
                    summon vex ~ ~ ~ {DeathLootTable:"empty",HandItems:[{id:"minecraft:air",count:1},{}],HandDropChances:[0.000F,0.000F]}
                }

                # Kill the marker
                kill @s
            }
        }

        # Guardian trap logic
        execute as @e[type=marker, tag=guardian_trap_marker] at @s run{
            particle dust{color:[0.000,1.000,0.000],scale:1} ~ ~ ~ .1 .1 .1 1 2 normal
            execute if entity @a[tag=!troller, distance=..3] run{
                particle minecraft:elder_guardian ~ ~ ~ 0 0 0 1 1 normal @a[distance=..3]
                playsound minecraft:entity.elder_guardian.curse master @a ~ ~ ~ 2 0.3
                kill @s
            }
        }
    }

    clock clock_5t 5t {
        # Setup the hole block
        execute as @e[type=endermite, tag=hole_block_spawner_marker] at @s run{
            setblock ~ ~ ~ orange_glazed_terracotta
            summon armor_stand ~ ~ ~ {Invisible:1b,Marker:1b,Tags:["hole_block_marker"]}
            kill @s
        }
    }
}

#! Forward and Backward stick handler
<%%
    let items = [
        `carrot_on_a_stick[custom_name='{"italic":false,"text":"Creeper Trap"}',lore=['"Creeper trap summons a creeper"','"when the player is nearby"'],custom_model_data=100001]`,
        `carrot_on_a_stick[custom_name='{"italic":false,"text":"Guardian Trap"}',lore=['"Show guardian jumpscare to nearby player"'],custom_model_data=100002]`,
        `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["hole_block_spawner_marker"]},custom_name='{"italic":false,"text":"Hole Block"}',lore=['"Spawns a worm when a"','"player passes by"'],custom_model_data=100003] `,

        `gold_block`,`diamond`,`redstone`,`egg`,`cake`,`emerald`,`iron_ingot`,`iron_block`,`diamond`,`egg`,`diamond`,`gold_block`
        ]
    global["ITEMS_LENGTH"] = items.length
    global["ITEMS"] = items
    console.log(global["ITEMS_LENGTH"] - 8) + 1
%%>
dir cycle_items{
    function cycle_item_check{
        # There are 9 slots for every player, 2 slots are reserved for the left and right arrows, Hence, 9 - 2 = 7 slots are available
        REPEAT(0, global["ITEMS_LENGTH"] - 7) as i{
            execute as @a[tag=troller] at @s if score @s item_select matches <%i%> run{
                <%%
                    for (let j = 1; j <= 7; j++) {
                        emit(`item replace entity @s hotbar.${j} with ${global["ITEMS"][(j+i)-1]}`)
                    }
                %%>
            }
        }
    }
    function give_items{
        item replace entity @s hotbar.0 with carrot_on_a_stick[minecraft:custom_model_data=111001, minecraft:item_name='{"text":"Previous"}'] 1
        scoreboard players set @s item_select 0
        function ./cycle_item_check
        item replace entity @s hotbar.8 with carrot_on_a_stick[minecraft:custom_model_data=111002, minecraft:item_name='{"text":"Next"}'] 1
    }
}
# function test {
#     <%%
#         global["Item"] = "Hello";
#         console.log(global)
#     %%>
#     <%%
#         console.log(global)
#     %%>
# }