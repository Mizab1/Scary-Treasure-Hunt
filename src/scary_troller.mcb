import ./templates/raycast.mcbt

dir private {
    
    #! Main functions
    function on_load minecraft:load{
        scoreboard objectives add mcb.internal dummy
        scoreboard objectives add rc_detect minecraft.used:minecraft.carrot_on_a_stick
        scoreboard objectives add item_select dummy
        scoreboard objectives add key_count dummy
        scoreboard objectives add private dummy

        scoreboard objectives add pos_x1 dummy
        scoreboard objectives add pos_y1 dummy
        scoreboard objectives add pos_z1 dummy

        scoreboard objectives add pos_x2 dummy
        scoreboard objectives add pos_y2 dummy
        scoreboard objectives add pos_z2 dummy

        team add troller
        team modify troller collisionRule never
    }
    function on_tick minecraft:tick {
        execute as @a[scores={rc_detect=1..}] at @s run{

            # Trap diffuser setup
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"trap_diffuser"}}}}] run{
                <%%
                    global["diffuser_radius"] = 2
                %%>
                kill @e[tag=scary_generated, distance=..<%global["diffuser_radius"]%>]
                fill ~-<%global["diffuser_radius"]/2%> ~-<%global["diffuser_radius"]/2%> ~-<%global["diffuser_radius"]/2%> ~<%global["diffuser_radius"]/2%> ~<%global["diffuser_radius"]/2%> ~<%global["diffuser_radius"]/2%> air replace #scary_troller:scary_blocks
            }

            # Random teleport logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"random_teleport_trap"}}}}, tag=!used_teleport_trap] run{
                spreadplayers ~ ~ 10 25 false @a[tag=!troller]
                tag @s add used_teleport_trap
            } else execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"random_teleport_trap"}}}}, tag=used_teleport_trap] run {
                tellraw @s {"text":"Slow down! you are using this trap too frequently...", "color":"red"}
            }

            # Scary Sound 1 logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"lidcreak_sound_trap"}}}}] run{
                playsound minecraft:scary.ambient.lidcreak master @a ~ ~ ~ 
            }

            # Scary Sound 2 logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"dark_slam_sound_trap"}}}}] run{
                playsound minecraft:scary.ambient.dark_slam master @a ~ ~ ~ 
            }

            # Scary Sound 3 logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"spooky_ambience_sound_trap"}}}}] run{
                playsound minecraft:scary.ambient.spooky_ambience master @a ~ ~ ~ 
            }

            # Scary Sound 4 logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"supernatural_sound_trap"}}}}] run{
                playsound minecraft:scary.ambient.supernatural master @a ~ ~ ~ 
            }

            # Freeze players trap logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"freeze_trap"}}}}, tag=!used_freeze_trap] run{
                <%%
                    global["freeze_player_trap_reach"] = 15
                    global["freeze_player_trap_duration"] = 4
                %%>
                execute as @a[tag=!troller, distance=..<%global["freeze_player_trap_reach"]%>] at @s run{
                    summon armor_stand ~ ~ ~ {Invisible:1b,Tags:["cage","scary_generated"],ArmorItems:[{},{},{},{id:"minecraft:wooden_hoe",count:1,components:{"minecraft:item_model":"cage"}}]}
                    effect give @s minecraft:slowness <%global["freeze_player_trap_duration"]%> 100 true
                    schedule <%global["freeze_player_trap_duration"]%>s replace {
                        kill @e[type=armor_stand,tag=cage]
                    }

                }
                tag @s add used_freeze_trap
            } else execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"freeze_trap"}}}}, tag=used_freeze_trap] run {
                tellraw @s {"text":"Slow down! You have just used this trap.", "color":"red"}
            }

            # Levitation wand logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"levitation_wand"}}}}] run{
                raycast levitation @s #minecraft:air @a[distance=..2,tag=!troller] {
                    effect give @a[distance=..2,tag=!troller] levitation 1 20 true
                }
            }

            # Blindness wand logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"blindness_wand"}}}}] run{
                raycast blindness @s #minecraft:air @a[distance=..2,tag=!troller] {
                    effect give @a[distance=..2,tag=!troller] darkness 4 1 true
                }
            }

            # Snowball gun logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"snowball_gun"}}}}] run{
                playsound minecraft:entity.snowball.throw master @a ~ ~ ~ 1 1.5
                summon snowball ^ ^1.5 ^ {Tags:["custom_snowball"]}
            }
            execute as @e[type=snowball,tag=custom_snowball,tag=!processed] at @s rotated as @p run{
                execute store result score @s pos_x1 run data get entity @s Pos[0] 1000
                execute store result score @s pos_y1 run data get entity @s Pos[1] 1000
                execute store result score @s pos_z1 run data get entity @s Pos[2] 1000

                tp @s ^ ^ ^0.1

                execute store result score @s pos_x2 run data get entity @s Pos[0] 1000
                execute store result score @s pos_y2 run data get entity @s Pos[1] 1000
                execute store result score @s pos_z2 run data get entity @s Pos[2] 1000

                scoreboard players operation @s pos_x2 -= @s pos_x1
                scoreboard players operation @s pos_y2 -= @s pos_y1
                scoreboard players operation @s pos_z2 -= @s pos_z1 

                execute store result entity @s Motion[0] double 0.03 run scoreboard players get @s pos_x2
                execute store result entity @s Motion[1] double 0.03 run scoreboard players get @s pos_y2
                execute store result entity @s Motion[2] double 0.03 run scoreboard players get @s pos_z2 

                tag @s add processed
            }
            
            # Laser gun logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"laser_gun"}}}}] run{
                playsound minecraft:block.beacon.deactivate master @a ~ ~ ~ 2 1.5
                raycast laser @s #minecraft:air @a[dx=0,tag=!troller] {
                    damage @a[dx=0,tag=!troller,limit=1] 1 minecraft:player_attack
                }
            }

            # Grenade launcher logic
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"grenade_launcher"}}}}] run{
                playsound minecraft:entity.dragon_fireball.explode master @a ~ ~ ~ 0.5 1.5
                summon tnt ^ ^1.5 ^ {Tags:["custom_grenade"],fuse:60}
            }
            execute as @e[type=tnt,tag=custom_grenade,tag=!processed] at @s rotated as @p run{
                execute store result score @s pos_x1 run data get entity @s Pos[0] 1000
                execute store result score @s pos_y1 run data get entity @s Pos[1] 1000
                execute store result score @s pos_z1 run data get entity @s Pos[2] 1000

                tp @s ^ ^ ^0.05

                execute store result score @s pos_x2 run data get entity @s Pos[0] 1000
                execute store result score @s pos_y2 run data get entity @s Pos[1] 1000
                execute store result score @s pos_z2 run data get entity @s Pos[2] 1000

                scoreboard players operation @s pos_x2 -= @s pos_x1
                scoreboard players operation @s pos_y2 -= @s pos_y1
                scoreboard players operation @s pos_z2 -= @s pos_z1 

                execute store result entity @s Motion[0] double 0.03 run scoreboard players get @s pos_x2
                execute store result entity @s Motion[1] double 0.03 run scoreboard players get @s pos_y2
                execute store result entity @s Motion[2] double 0.03 run scoreboard players get @s pos_z2 

                tag @s add processed
            }

            # Backward and forward button
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"previous_arrow"}}}}] run{
                execute if score @s item_select matches 1.. run{
                    scoreboard players remove @s item_select 1
                    playsound minecraft:block.stone_button.click_off master @s ~ ~ ~ 1 1
                    function *private/cycle_items/cycle_item_check
                }
                execute if score @s item_select matches 0 run{
                    playsound minecraft:block.note_block.didgeridoo master @s ~ ~ ~ 0.5 1.2
                }
            }
            execute if entity @s[nbt={SelectedItem:{id:"minecraft:carrot_on_a_stick",count:1,components:{"minecraft:custom_data":{tool:"next_arrow"}}}}] run{
                execute if score @s item_select matches ..<%global["ITEMS_LENGTH"] - 8%> run{
                    scoreboard players add @s item_select 1
                    playsound minecraft:block.stone_button.click_on master @s ~ ~ ~ 1 1
                    function *private/cycle_items/cycle_item_check
                }
                execute if score @s item_select matches <%global["ITEMS_LENGTH"] - 8 + 1%> run{
                    playsound minecraft:block.note_block.didgeridoo master @s ~ ~ ~ 0.5 1.2
                }
            }

            # Reset click
            scoreboard players reset @s rc_detect
        }

        # Teleport the troller entity to the troller
        function ./troller_entity/teleport_entity_to_troller
        execute as @a[tag=troller] at @s unless entity @e[type=mutantmonsters:mutant_enderman,tag=troller_entity,distance=..4] run function ./troller_entity/summon_entity
        execute as @e[type=mutantmonsters:mutant_enderman,tag=troller_entity] at @s unless entity @a[distance=..4] run kill @s

        # Check score and open the door
        execute if score key_inserted private matches 6.. run{
            function ./door/open_door
        }
    } 

    #! Clocks
    dir clocks {

        clock clock_160t 160t{
            tag @a[tag=used_freeze_trap] remove used_freeze_trap
        }
        clock clock_80t 80t{
            tag @a[tag=used_teleport_trap] remove used_teleport_trap
        }
        clock clock_20t 20t {
            # Hole block logic
            execute as @e[type=armor_stand, tag=hole_block_marker] at @s run{
                execute if entity @a[tag=!troller, distance=..3] unless entity @e[type=silverfish, tag=hole_block_worm, distance=..3] run{
                    summon silverfish ~ ~1 ~ {Tags:["hole_block_worm", "scary_generated"],CustomName:'"Blood Worm"'}
                }
                execute unless block ~ ~ ~ orange_glazed_terracotta run{
                    kill @s
                }
            }

            # Water trap logic
            execute as @e[type=marker, tag=water_trap] at @s run{
                execute if entity @a[tag=!troller, distance=..1] run{
                    REPEAT (-1, 1) as i {
                        REPEAT (-1, 1) as j {
                            summon evoker_fangs ~<%i%> ~ ~<%j%>
                        }
                    }
                }
            }
        }

        clock clock_10t 10t {
            # Creeper trap logic
            execute as @e[type=marker, tag=creeper_trap_marker] at @s run{
                particle dust{color:[1.000,0.000,0.000],scale:1} ~ ~ ~ .1 .1 .1 1 2 normal
                execute if entity @a[tag=!troller, distance=..3] run{
                    summon creeper ~ ~ ~ {CustomName:'"Scary Creeper"',DeathLootTable:"empty", Tags:["scary_generated"]}
                    REPEAT (1, 4) {
                        summon vex ~ ~ ~ {CustomName:'"Ghost"',DeathLootTable:"empty",Tags:["scary_generated"],HandItems:[{id:"minecraft:air",count:1},{}],HandDropChances:[0.000F,0.000F]}
                    }

                    # Kill the marker
                    kill @s
                }
            }

            # Guardian trap logic
            execute as @e[type=marker, tag=guardian_trap_marker] at @s run{
                particle dust{color:[0.000,1.000,0.000],scale:1} ~ ~ ~ .1 .1 .1 1 2 normal
                execute if entity @a[tag=!troller, distance=..3] run{
                    particle minecraft:elder_guardian ~ ~ ~ 0 0 0 1 1 normal @a[distance=..3]
                    playsound minecraft:entity.elder_guardian.curse master @a ~ ~ ~ 2 0.3
                    kill @s
                }
            }

            # Landmine trap logic
            execute as @e[type=armor_stand, tag=landmine] at @s run{
                execute if entity @a[tag=!troller, distance=..2] run{
                    summon creeper ~ ~ ~ {Fuse:0, Tags:["scary_generated"]}
                    kill @s
                }
            }

            # Fire stone logic 
            execute as @e[type=armor_stand, tag=fire_stone] at @s run{
                execute unless block ~ ~ ~ light_blue_terracotta run{
                    fill ~3 ~3 ~3 ~-3 ~-3 ~-3 fire replace air
                    kill @s
                }
            }
        }

        clock clock_5t 5t {
            # Setup the creeper trap
            execute as @e[type=endermite, tag=creeper_trap_spawner_marker] at @s run{
                summon marker ~ ~ ~ {Tags:["creeper_trap_marker", "scary_generated"]}
                kill @s
            }

            # Setup the guardian trap
            execute as @e[type=endermite, tag=guardian_trap_spawner_marker] at @s run{
                summon marker ~ ~ ~ {Tags:["guardian_trap_marker", "scary_generated"]}
                kill @s
            }

            # Setup the hole block
            execute as @e[type=endermite, tag=hole_block_spawner_marker] at @s run{
                setblock ~ ~ ~ orange_glazed_terracotta
                summon armor_stand ~ ~ ~ {Invisible:1b,Marker:1b,Tags:["hole_block_marker", "scary_generated"]}
                kill @s
            }

            # Setup the landmine
            execute as @e[type=endermite, tag=landmine_spawner_marker] at @s run{
                summon armor_stand ~ ~ ~ {Invisible:1b,Tags:["landmine", "scary_generated"],ArmorItems:[{},{},{},{id:"minecraft:endermite_spawn_egg",count:1,components:{"minecraft:item_model":"landmine_trap"}}]}
                kill @s
            }

            # Setup the fire stone
            execute as @e[type=endermite, tag=fire_stone_spawner_marker] at @s run{
                setblock ~ ~ ~ light_blue_terracotta
                summon armor_stand ~ ~ ~ {Invisible:1b,Tags:["fire_stone", "scary_generated"], Marker:1b}
                kill @s
            }

            # Setup the water trap
            execute as @e[type=endermite, tag=water_spawner_marker] at @s run{
                execute if block ~ ~ ~ water run{
                    summon marker ~ ~ ~ {Tags:["water_trap", "scary_generated"]}
                    kill @s
                } else run {
                    tellraw @a[tag=troller] {"text":"[Water Trap] Trap can't be set up because it's not in a water body", "color":"red"}
                    kill @s
                }
            }

            # Setup the herobrine trap
            execute as @e[type=endermite, tag=herobrine_spawner_marker] at @s run{
                summon armor_stand ~ ~ ~ {Invisible:1b,Tags:["new","herobrine_trap", "scary_generated"], ArmorItems:[{},{},{},{id:"minecraft:wooden_hoe",count:1,components:{"minecraft:item_model":"herobrine_trap"}}]}
                execute as @e[type=armor_stand, tag=herobrine_trap, tag=new, distance=..3] at @s run{
                    # rotate @s facing entity @p[tag=troller]
                    tag @s remove new
                }
                kill @s
            }

            # Count the number of key for each player and display corresponding number of custom key font to them
            execute as @a run {
                # Store the count of the key(s)
                execute store result score @s key_count run clear @s carrot_on_a_stick[custom_model_data=111003] 0

                REPEAT (1, 6) as i {
                    execute if score @s key_count matches <%i < 5? i: i + ".."%> run title @s actionbar {"text":"<%"\\uE000".repeat(i)%>"}
                }
            }

            # Check for player presence near the teleport marker
            execute as @e[type=marker,tag=teleport_marker] at @s if entity @a[distance=..2] run{
                tp @a -158 277 -184 -180 0
                execute as @a at @s run playsound minecraft:ui.toast.challenge_complete master @s ~ ~ ~ 1

                # Fireworks
                summon firework_rocket -162 277 -189 {LifeTime:15,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"small_ball",has_twinkle:true,colors:[I;4259648,9205247,8060878]}]}}}}
                summon firework_rocket -162 277 -188 {LifeTime:12,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"burst",colors:[I;10747842,16739297,8257427]}]}}}}
                summon firework_rocket -161 277 -189 {LifeTime:13,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"burst",colors:[I;10747842,16739297,8257427]}]}}}}

                summon firework_rocket -154 277 -189 {LifeTime:15,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"small_ball",has_twinkle:true,colors:[I;4259648,9205247,8060878]}]}}}}
                summon firework_rocket -155 277 -189 {LifeTime:12,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"burst",colors:[I;10747842,16739297,8257427]}]}}}}
                summon firework_rocket -154 277 -188 {LifeTime:13,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"burst",colors:[I;10747842,16739297,8257427]}]}}}}

                summon firework_rocket -154 277 -181 {LifeTime:15,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"small_ball",has_twinkle:true,colors:[I;4259648,9205247,8060878]}]}}}}
                summon firework_rocket -155 277 -181 {LifeTime:12,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"burst",colors:[I;10747842,16739297,8257427]}]}}}}
                summon firework_rocket -154 277 -182 {LifeTime:13,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"burst",colors:[I;10747842,16739297,8257427]}]}}}}

                summon firework_rocket -162 277 -181 {LifeTime:15,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"small_ball",has_twinkle:true,colors:[I;4259648,9205247,8060878]}]}}}}
                summon firework_rocket -161 277 -181 {LifeTime:12,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"burst",colors:[I;10747842,16739297,8257427]}]}}}}
                summon firework_rocket -162 277 -182 {LifeTime:13,FireworksItem:{id:"minecraft:firework_rocket",count:1,components:{"minecraft:fireworks":{explosions:[{shape:"burst",colors:[I;10747842,16739297,8257427]}]}}}}
            }
        }
    }

    #! Forward and Backward stick handler
    <%%
        let items = [
            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["creeper_trap_spawner_marker"]},custom_name='{"italic":false,"text":"Creeper Trap"}',lore=['"Summons a creeper when the player is nearby"'],custom_data={tool:"creeper_trap"},custom_model_data=100001] `,

            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["guardian_trap_spawner_marker"]},custom_name='{"italic":false,"text":"Guardian Trap"}',lore=['"Displays the guardian particle to the nearby player"'],custom_data={tool:"guardian_trap"},custom_model_data=100002] `,
            
            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["hole_block_spawner_marker"]},custom_name='{"italic":false,"text":"Hole Block"}',lore=['"Spawns a worm when a"','"player passes by"'],custom_data={tool:"hole_block_trap"},custom_model_data=100003] `,

            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["landmine_spawner_marker"]},custom_name='{"italic":false,"text":"Landmine"}',lore=['"Places a landmine which explodes"','"when a player steps on it"'],custom_data={tool:"landmine_trap"},custom_model_data=100004] `,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Trap Diffuser"}',lore=['"Disable all the traps within ${global["diffuser_radius"]} blocks radius."'],custom_data={tool:"trap_diffuser"},custom_model_data=100005]`,

            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["fire_stone_spawner_marker"]},custom_name='{"italic":false,"text":"Fire Stone"}',lore=['"Places a stone which when mined sets"','"the nearby area ablaze"'],custom_data={tool:"fire_stone_trap"},custom_model_data=100006] `,

            `stick[enchantment_glint_override=true,custom_name='{"italic":false,"text":"Knockback Stick"}',lore=['"Deals humongous amount of knockback"','"to enemies"'],enchantments={levels:{"minecraft:knockback":25},show_in_tooltip:false}]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Random Teleport"}',lore=['"Randomly teleport all the enemies."'],custom_data={tool:"random_teleport_trap"},custom_model_data=100007]`,

            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["water_spawner_marker"]},custom_name='{"italic":false,"text":"Water Trap"}',lore=['"Summons fangs in water when a player is nearby"','"Can only be placed in water"'],custom_data={tool:"water_trap"},custom_model_data=100008] `,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Play Scary Sound 1"}',lore=['"Right-click to play a scary sound at your position."'],custom_data={tool:"lidcreak_sound_trap"},custom_model_data=100009]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Play Scary Sound 2"}',lore=['"Right-click to play a scary sound at your position."'],custom_data={tool:"dark_slam_sound_trap"},custom_model_data=100010]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Play Scary Sound 3"}',lore=['"Right-click to play a scary sound at your position."'],custom_data={tool:"spooky_ambience_sound_trap"},custom_model_data=100011]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Play Scary Sound 4"}',lore=['"Right-click to play a scary sound at your position."'],custom_data={tool:"supernatural_sound_trap"},custom_model_data=100012]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Cage Trap"}',lore=['"Right-click to cage nearby players,"', '"up to ${global["freeze_player_trap_reach"]} blocks."'],custom_data={tool:"freeze_trap"},custom_model_data=100013]`,

            `endermite_spawn_egg[entity_data={id:"minecraft:endermite",Silent:1b,NoAI:1b,Tags:["herobrine_spawner_marker"]},custom_name='{"italic":false,"text":"Herobrine Trap"}',lore=['"Summons herobrine statue when a player is nearby"','"Rotated as the troller"'],custom_data={tool:"herobrine_trap"},custom_model_data=100014] `,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Levitation Wand"}',lore=['"Right-click to levitate the player you are aiming at"'],custom_data={tool:"levitation_wand"},custom_model_data=100015]`,

            `carrot_on_a_stick[custom_name='{"italic":false,"text":"Blindness Wand"}',lore=['"Right-click to blind the player you are aiming at"'],custom_data={tool:"blindness_wand"},custom_model_data=100016]`,
        ]
        global["ITEMS_LENGTH"] = items.length
        global["ITEMS"] = items
        console.log(global["ITEMS_LENGTH"] - 8) + 1
    %%>
    dir cycle_items{
        function cycle_item_check{
            # There are 9 slots for every player, 2 slots are reserved for the left and right arrows, Hence, 9 - 2 = 7 slots are available
            REPEAT(0, global["ITEMS_LENGTH"] - 7) as i{
                execute as @a[tag=troller] at @s if score @s item_select matches <%i%> run{
                    <%%
                        for (let j = 1; j <= 7; j++) {
                            emit(`item replace entity @s hotbar.${j} with ${global["ITEMS"][(j+i)-1]}`)
                        }
                    %%>
                }
            }
        }
        function give_items{
            item replace entity @s hotbar.0 with carrot_on_a_stick[custom_model_data=111001, custom_data={tool:"previous_arrow"}, minecraft:item_name='{"text":"Previous"}'] 1
            scoreboard players set @s item_select 0
            function ./cycle_item_check
            item replace entity @s hotbar.8 with carrot_on_a_stick[custom_model_data=111002, custom_data={tool:"next_arrow"}, minecraft:item_name='{"text":"Next"}'] 1
        }
    }
    dir troller_entity{
        function summon_entity{
            summon mutantmonsters:mutant_enderman ~ ~ ~ {NoAI:1b,Tags:["troller_entity"],Team:"troller",DeathLootTable:"empty"}
        }
        function teleport_entity_to_troller{
            execute as @a[tag=troller] at @s run{
                execute rotated ~ 0 positioned ^ ^ ^-1 run tp @e[type=mutantmonsters:mutant_enderman,tag=troller_entity,limit=1,sort=nearest] ~ ~ ~ ~ ~
            }
            # execute as @e[type=mutantmonsters:mutant_enderman,tag=troller_entity] at @s run{
            #     execute if entity @a[tag=troller,distance=..4] run{
            #         execute rotated 0 0 positioned ^ ^ ^-1 run tp @s @a[tag=troller,distance=..4,limit=1,sort=nearest]
            #     } else execute unless entity @a[tag=troller,distance=..4] run {
            #         tp @s ~ ~-600 ~
            #     }
            # }
        }
        function kill_troller_entity{
            kill @e[type= mutantmonsters:mutant_enderman]
            kill @e[type= mutantmonsters:endersoul_clone]
            kill @e[type= mutantmonsters:endersoul_fragment]
        }
    }
    dir door{
        #! WARNING: You have to manually place the door with all of it blocks and entities like the marker and interaction entities

        function spawn_key_holder{
            # fill -155 94 -179 -155 94 -179 prismarine
            summon interaction ~ ~ ~ {width:1.05f,height:1.05f,Tags:["key_holder"]}
        }
        function spawn_door_marker{
            summon marker ~ ~ ~ {Tags:["door_marker"]}
        }
        function spawn_teleport_marker{
            summon marker ~ ~ ~ {Tags:["teleport_marker"]}
        }
        function place_door_structure{
            function ./kill_door_entities

            setblock -155 96 -181 air
            setblock -155 96 -181 bedrock

            setblock -155 96 -178 air
            setblock -155 96 -178 bedrock

            # Place the key holders with their respective entities
            <%%
                let key_holder_coords = [
                    `-155 94 -181`,
                    `-155 95 -181`,

                    `-155 96 -180`,
                    `-155 96 -179`,

                    `-155 95 -178`,
                    `-155 94 -178`
                ]
                key_holder_coords.forEach(i => {
                    emit(`setblock ${i} air`)
                    emit(`setblock ${i} blackstone`)
                    emit.mcb(`execute positioned ${i} run function ./spawn_key_holder`)
                })

                let door_coords = [
                    `-155 94 -179`,
                    `-155 94 -180`
                ]
                door_coords.forEach((i, idx) => {
                    let [x, y, z] = i.split(" ")
                    
                    emit(`setblock ${i} air`)
                    emit.mcb(`execute positioned ${i} run function ./spawn_door_marker`)

                    if (idx === 0){
                        emit(`setblock ${i} iron_door[half=lower,open=true,hinge=right]`)
                        emit(`setblock ${x} ${+y+1} ${z} iron_door[half=upper,open=true,hinge=right]`)
                    } else {
                        emit(`setblock ${i} iron_door[half=lower,open=true,facing=south]`)
                        emit(`setblock ${x} ${+y+1} ${z} iron_door[half=upper,open=true,facing=south]`)
                    }
                })
            %%>
            execute positioned -158.8 94 -179 run function ./spawn_teleport_marker
        }
        function open_door{
            execute as @e[type=marker,tag=door_marker] at @s run{
                playsound minecraft:item.trident.thunder master @a ~ ~ ~ 1 0.5

                setblock ~ ~ ~ air
                setblock ~ ~1 ~ air

                kill @s
            }
        }
        function kill_door_entities{
            # fill -155 94 -179 -155 94 -179 air
            kill @e[type=interaction,tag=key_holder]
            kill @e[type=marker,tag=door_marker]
            kill @e[type=marker,tag=teleport_marker]
            scoreboard players set key_inserted private 0
        }
    }
    dir click_detection{
        function right_click_detection{
            advancement revoke @s only <%context["namespace"]%>:right_click_detection

            tag @s add this
            # execute if data entity @s SelectedItem{id:"minecraft:carrot_on_a_stick",components:{"minecraft:custom_data":{tool:"key"}}} run{
            # }
            execute if data entity @s SelectedItem{id:"minecraft:carrot_on_a_stick",components:{"minecraft:custom_data":{tool:"key"}}} \
                as @e[type=interaction,tag=key_holder,tag=!activated,distance=..6] \
                at @s \
                run{
                    function ./find_targeted
                }
            execute as @e[type=interaction,tag=key_holder] run data remove entity @s interaction
            tag @s remove this
        }
        function find_targeted{
            scoreboard players set #bool private 0
            execute on target run{
                clear @s minecraft:carrot_on_a_stick[minecraft:custom_data={tool:"key"}] 1
                execute store result score #bool private if entity @s[tag=this]
            }

            execute if score #bool private matches 1 run{
                execute unless score key_inserted private matches 0.. run scoreboard players set key_inserted private 0
                scoreboard players add key_inserted private 1

                playsound minecraft:block.vault.open_shutter master @a ~ ~ ~ 1 0.5
                particle happy_villager ~ ~2 ~ 0 0 0 0 1
                tag @s add activated
                fill ~ ~ ~ ~ ~ ~ dark_prismarine
                data remove entity @s interaction
            }
        }
    }
    dir celebration_room{
        function create_text{
            summon text_display -158 278 -190 {billboard:"fixed",Tags:["completion_text"],transformation:{left_rotation:[0f,0f,0f,1f],right_rotation:[0f,0f,0f,1f],translation:[0f,0f,0f],scale:[2f,2f,2f]},text:'{"color":"dark_blue","text":"Hope you\'ve enjoyed!"}',background:16711680}
            summon text_display -158 279 -190 {billboard:"fixed",Tags:["completion_text"],transformation:{left_rotation:[0f,0f,0f,1f],right_rotation:[0f,0f,0f,1f],translation:[0f,0f,0f],scale:[2f,2f,2f]},text:'{"color":"dark_blue","text":"Thanks for playing!"}',background:16711680}

            # Restart
            summon text_display -158.7 278.35 -179.1 {Tags:["restart_text"],transformation:{left_rotation:[0f,180f,0f,1f],right_rotation:[0f,0f,0f,1f],translation:[0f,0f,0f],scale:[1f,1f,1f]},text:'{"color":"black","text":"<- Restart"}',background:16711680}
        }
    }
    dir start_room{
        function create_text{
            summon text_display -178 278.35 -189.9 {Tags:["start_text"],text:'{"color":"black","text":"<- Start"}',background:16711680}
        }
    }
    function run_on_first_tick{
        #! This will only run as a player and on their first tick
        # /advancement revoke Mizab only scary_troller:run_on_first_tick
        # The above command is used to revoke this advancement

        spawnpoint @s -179 277 -184
        tp @s -179 277 -184 120 0
    }
}

function make_me_troller{
    tag @s add troller
    function *private/cycle_items/give_items
    team join troller @s
    effect give @s minecraft:invisibility infinite 1 true
    # function *private/troller_entity/summon_entity
}
function remove_troller{
    tag @a[tag=troller] remove troller
    effect clear @s minecraft:invisibility
    team leave @s
    clear @s
    kill @e[type= mutantmonsters:mutant_enderman,tag=troller_entity]
}
function give_non_troll_weapons{
    give @s iron_sword[custom_name='{"italic":false,"text":"Super Sword"}',lore=['"Gives increased strength and"','"increased speed to the bearer"'],attribute_modifiers={modifiers:[{id:"attack_damage",type:"generic.attack_damage",amount:6,operation:"add_value"},{id:"movement_speed",type:"generic.movement_speed",amount:0.1,operation:"add_value"}],show_in_tooltip:false}] 1
    give @s crossbow[custom_name='{"italic":false,"text":"Rapid-fire Crossbow"}',lore=['"A special crossbow for shooting"','"arrows in a quick succession"'],enchantments={levels:{"minecraft:quick_charge":100},show_in_tooltip:false}] 1
    give @s carrot_on_a_stick[custom_name='{"italic":false,"text":"Laser Gun"}',lore=['"Right-click to shoot laser beam"'],custom_data={tool:"laser_gun"},custom_model_data=100051] 1
    give @s carrot_on_a_stick[custom_name='{"italic":false,"text":"Snowball Gun"}',lore=['"Right-click to shoot snowball"'],custom_data={tool:"snowball_gun"},custom_model_data=100052] 1
    give @s carrot_on_a_stick[custom_name='{"italic":false,"text":"Grenade Launcher"}',lore=['"Right-click to shoot grenade"'],custom_data={tool:"grenade_launcher"},custom_model_data=100053] 1
    give @s arrow 64
}
function give_key{
    give @s carrot_on_a_stick[custom_name='{"italic":false,"text":"Key"}',custom_data={tool:"key"},custom_model_data=111003]
}

#! JSONs
tag blocks scary_blocks replace {
    orange_glazed_terracotta
    light_blue_terracotta
}
advancement right_click_detection {
    "criteria": {
        "requirement": {
            "trigger": "minecraft:player_interacted_with_entity",
            "conditions": {
                "entity": {
                    "type": "interaction"
                }
            }
        }
    },
    "rewards": {
        "function": "scary_troller:private/click_detection/right_click_detection"
    }
}
advancement run_on_first_tick{
    "criteria": {
        "": {
            "trigger": "minecraft:tick"
        }
    },
    "rewards": {
        "function": "scary_troller:private/run_on_first_tick"
    }
}

# function test {
#     <%%
#         global["Item"] = "Hello";
#         console.log(global)
#     %%>
#     <%%
#         console.log(global)
#     %%>
# }